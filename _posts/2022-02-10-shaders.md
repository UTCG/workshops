<style>
    .red > code {
        color: #FF8080;
    }
    .blue > code {
        color: #4080FF;
    }
</style>

# UTCG Shaders Workshop

## Intro

### What are we gonna cover?

This workshop covers the basics of shader programming in GLSL. We'll only be making 2D graphics. We're hoping that by the end of the workshop, you have a good enough grasp on shader programming that you can make your own animated noise art, and get started working on more complicated shaders for your own graphics and game development projects (or Minecraft).

We hope that you follow along through the code during the workshop, whether you're writing it yourself with us or uncommenting lines as we write them. Below is all the code we'll be using:

- Part 1: Mandelbrot
  - Minimal starter code: [https://www.shadertoy.com/view/NdSyW3](https://www.shadertoy.com/view/NdSyW3)
  - Starter code with finished example commented out: [https://www.shadertoy.com/view/NdsyDX](https://www.shadertoy.com/view/NdsyDX)
  - Finished code: [https://www.shadertoy.com/view/fsSyW3](https://www.shadertoy.com/view/fsSyW3)
- Part 2: Clock
  - Minimal starter code: [https://www.shadertoy.com/view/NsSyW3](https://www.shadertoy.com/view/NsSyW3)
  - Starter code with finished example commented out: [https://www.shadertoy.com/view/fsByW3](https://www.shadertoy.com/view/fsByW3)
  - Finished code: [https://www.shadertoy.com/view/fdByW3](https://www.shadertoy.com/view/fdByW3)
- Part 3: Noise art
  - Starter code: [https://www.shadertoy.com/view/7s2yzy](https://www.shadertoy.com/view/7s2yzy)
  - Example finished code: [https://www.shadertoy.com/view/ssScDw](https://www.shadertoy.com/view/ssScDw)
- Competition
  - Starter code: [https://www.shadertoy.com/view/sdSczG](https://www.shadertoy.com/view/sdSczG)

### What is a shader?

A *pixel shader* (aka *fragment shader*) is code that runs for each pixel being rendered. There are other types of shaders (e.g. vertex shaders, geometry shaders, etc) but we won't cover them in this workshop. Generally, they're code that runs on the GPU, usually for rendering graphics. In games, they're usually used for lighting effects (they are called shaders) and other fancy effects at the end of the rendering pipeline. GPUs are great for shaders, because they're made for doing similar work for lots of different inputs - in this case, they'll run the same shader code for each pixel on the screen.

### Coding shaders

Shaders are usually written in *GLSL*, which is *OpenGL Shading Language*. It's kind of like C/C++. This is the language that Minecraft shaders use!

One important thing to note is that it does not automatically convert between integers and floats. So it will throw an error if you do something like `float x = 2;` or `float y = M_PI / 2`. To deal with this, always put a `.` after an integer, e.g. `float x = 2.` and `float y = M_PI / 2.`.

## Fragment shader basics, Transformations, Colour - Mandelbrot Set

To learn the basics, let's start by rendering the [Mandelbrot set](https://en.wikipedia.org/wiki/Mandelbrot_set). We've provided some starter code, linked below. We won't be paying much attention to the mathematics behind the Mandelbrot set, so the `mandel()` function is provided (and you don't need to understand it).

<iframe width="640" height="360" frameborder="0" src="https://www.shadertoy.com/embed/fsSyW3?gui=true&t=10&paused=true&muted=false" allowfullscreen></iframe>

Code:

- Minimal starter code: [https://www.shadertoy.com/view/NdSyW3](https://www.shadertoy.com/view/NdSyW3)
- Starter code with finished example commented out: [https://www.shadertoy.com/view/NdsyDX](https://www.shadertoy.com/view/NdsyDX)
- Finished code: [https://www.shadertoy.com/view/fsSyW3](https://www.shadertoy.com/view/fsSyW3)

### What does our shader need to do?

Our fragment shader's input is a pixel on the screen and its output is its colour, as a 4D RGBA (Red/Green/Blue/Alpha) vector.

```glsl
void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    ...
}
```

We've given a variable `vec2 uv` to represent the transformed coordinates of a pixel. We'll use it and `mandel()` to calculate the colour for each pixel.

```glsl
void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = fragCoord/iResolution.x;

    // mandel returns a value between 0 and 1
    float value = mandel(uv.x, uv.y);

    vec3 col = vec3(value, value, value);

    fragColor = vec4(col, 1.);
}
```

Since we've put `value` into the R/G/B parts of `col`, the resulting image should be in greyscale.

### Transformation

We mentioned that `uv` are the transformed coordinates. You might recall from high school that we can transform coordinates by multiplying/dividing our vector by a number to stretch, or by adding/subtracting another vector to shift. Try doing this yourself to get the whole set in the frame!

```glsl
void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = vec2(fragCoord.x / hrztl_stretch - hrztl_shift,
                   fragCoord.y / vrtcl_stretch - vrtcl_shift);

    ...
}
```

### Working with colour - `mix()`

We can make other colours by putting different values into the R/G/B parts of `col`, e.g. <span class="red">`red = vec3(1., .5, .5)`</span> or <span class="blue">`blue = vec3(.25, .5, 1.)`</span>.

We can also interpolate between colours (or really anything) using the GLSL function `mix()`. The first two arguments are the values we want to interpolate between, and the last is the "slider value" to "slide" between the two, from `0` to `1`.

```glsl
vec3 red = vec3(1., .5, .5);
vec3 blue = vec3(.25, .5, 1.);
vec3 col = mix(red, blue, mandel);
```

### Extras

Throw `sin()`, `cos()`, and `iTime` into your colour calculation and we've got a rave going!

## Rotation, Time, Circles - Analogue Clock

Our next example is an analogue clock.

<iframe width="640" height="360" frameborder="0" src="https://www.shadertoy.com/embed/fdByW3?gui=true&t=10&paused=false&muted=false" allowfullscreen></iframe>

Code:

- Minimal starter code: [https://www.shadertoy.com/view/NsSyW3](https://www.shadertoy.com/view/NsSyW3)
- Starter code with finished example commented out: [https://www.shadertoy.com/view/fsByW3](https://www.shadertoy.com/view/fsByW3)
- Finished code: [https://www.shadertoy.com/view/fdByW3](https://www.shadertoy.com/view/fdByW3)

Note that this time we've transformed the pixel coordinates for you, so that the centre of the screen has coordinates $(0, 0)$.

### Creating the clock face

Clocks are circles. This is basic anthropology. But what is a circle?

A circle is all the points within some radius $r$ of the centre of the circle $\mathbf c$. So if we have a pixel whose coordinates are $\mathbf p = (x, y)$, then we can check if the pixel is within the circle by checking if $\text{length}(\mathbf p - \mathbf c) < r$. Let's make a function to do that using the GLSL `length()` function:

```
bool isInCircle(vec2 centre, float radius, vec2 point)
{
    // return true if point is closer to centre than r
    return length(point - centre) < radius;
}
```

Let's set the centre of our clock to be the centre of the screen, and give it a radius of 0.4:

```glsl
vec2 clockCentre = vec2(0, 0);
float clockRadius = 0.4;
```

And now, let's use our circle function to colour the clock face red:

```glsl
if (isInCircle(clockCentre, clockRadius, uv))
{
    col = vec3(1., 0.5, 0.5);
}
```

If you render now, you should see a big red circle in the middle of the screen!

### Initializing the seconds hand

The initial position of a clock hand, at time 0 is pointing straight up, so let's create an `up` vector to represent that:

```glsl
vec2 up = vec2(0, 0.2);
```

Now let's render it as a small white dot, the same way we rendered the clock face:

```glsl
vec2 secondsHand = up;

float handRadius = 0.02;

// put the following code AFTER the clock face colour code

if (isInCircle(secondsHand, handRadius, uv))
{
    col = vec3(1, 1, 1);
}
```

### Making it move

Using linear algebra, we've created a `rot()` function which returns a matrix that will rotate a 2D vector clockwise by a given angle in radians ([more on rotation matrices](https://en.wikipedia.org/wiki/Rotation_matrix)). To get a rotated vector, you multiply a rotation matrix times a vector. For example,

```glsl
vec2 northEast = vec2(1, 1);
vec2 southEast = rot(0.5 * M_PI) * northEast;
```

In our case, we want to rotate our clock hand by some angle based on time. GLSL gives us a handy variable `iTime`, which is the time in seconds since we started the shader. Let's try it out by editing our seconds hand's position:

```glsl
vec2 secondsHand = rot(iTime) * up;
```

### Getting the speed right

Great, it's rotating! But it's the wrong speed. One full rotation is $2\pi$ (aka `2. * M_PI`) radians, and it takes 60 seconds, so we'll rotate our seconds hand correspondingly:

```glsl
float seconds = iTime;

// 60 seconds for a rotation
vec2 secondsHand = rot(seconds / 60. * 2. * M_PI) * up;
```

Now we have a seconds hand! Let's try a minutes hand next, using the same strategy:

```glsl
float minutes = seconds / 60.

// 60 mins for a rotation
vec2 minutesHand = rot(minutes / 60. * 2. * M_PI) * up;

...

if (isInCircle(minutesHand, handRadius, uv))
{
    col = vec3(1, 1, 1);
}
```

Try doing an hour hand yourself!

### Extras

To make the clock show the actual time, take a look at the GLSL `iDate` variable. Note that this is a 4D vector. To make it even more clock-like, see if you can use the GLSL `floor()` function to make the seconds hand "tick" instead of rotating smoothly.

You can also make it look better by changing the lengths of the hands, so that they never overlap. Other aesthetic upgrades could be a bezel, or patterns on the clock face. Numbers are a bit more difficult, but look into textures if you have some time and wanna try it out.

## Using `sin()` and `cos()` creatively - Noise art

The goal of this workshop was to give you enough shaders knowledge to make cool noise art, like this:

<iframe width="640" height="360" frameborder="0" src="https://www.shadertoy.com/embed/ssScDw?gui=true&t=10&paused=false&muted=false" allowfullscreen></iframe>

How do we do this?

...

Just kinda messing around tbh.

Code:

- Starter code for example: [https://www.shadertoy.com/view/7s2yzy](https://www.shadertoy.com/view/7s2yzy)
- Example finished code: [https://www.shadertoy.com/view/ssScDw](https://www.shadertoy.com/view/ssScDw)
- Competition starter code: [https://www.shadertoy.com/view/sdSczG](https://www.shadertoy.com/view/sdSczG)

### Creating patterns with functions

We know we can `mix()` two colours as long as we have a value between `0` and `1`. And since the trigonometric functions `sin()` and `cos()` are periodic, we can use them to create patterns. We can also combine them in weird ways to get unexpected patterns.

```glsl
float noiseFunc(float x, float y)
{
    return something;
}
```

![Sine and Cosine graph by Geek3 from Wikipedia](https://upload.wikimedia.org/wikipedia/commons/7/71/Sine_cosine_one_period.svg)

`sin()` and `cos()` both range between `-1` and `1`, so we need to transform them to the range we want.

```glsl
float noiseFunc(float x, float y)
{
    float val = cos(...);

    // transform cos value so that it's between 0 and 1
    return 0.5 + 0.5 * val;
}
```

What do we put inside that `cos()` function? Anything basically. Go crazy. Just do random stuff. Better if you mix `x`, `y`, `iTime`, more trigonometry, etc in there. Here's what I did for the example above:

```glsl
float noiseFunc(float x, float y)
{
    float val = cos(x
                    + sin(2. * y + cos(x + iTime))
                    + cos(iTime + x + cos(4. * y))
                    + 10. * sin(x)
                    );

    // transform cos value so that it's between 0 and 1
    return 0.5 + 0.5 * val;
}
```

Feel free to mix more colours, to use if/else to render different things for different parts of the screen, use the functions we provided earlier such as `mandel()`, `rot()`, and `isInCircle`, and etc. Go crazy.

When you're done, submit your shader to the competition by listing it as "public" and adding the tag `utcg2022`.

## Where to learn more

[The Book of Shaders](https://thebookofshaders.com/) is an incomplete resource on GLSL programming, but the pages it does have are excellent quality and easy to follow. They're especially great if you're interested in more complex noise art, like [this](https://thebookofshaders.com/edit.php#11/lava-lamp.frag) from page 11.

The real heavyweight in shaders is Inigo Quiles, co-creator of Shadertoy. [His website](https://www.iquilezles.org/index.html) has articles, tutorials, demos, and more of graphics programming, including more noise art like [this](https://www.iquilezles.org/www/articles/warp/warp.htm).

There are many more tutorials on shaders that are just a search away, since GLSL is a very widely-used technology. Plus, they're covered a little bit in [CSC317 - Computer Graphics](https://github.com/alecjacobson/computer-graphics-csc317) (formerly CSC418).

## Thanks!

If you enjoyed this workshop, make sure you follow us on social media, join our mailing list, and join our Discord to keep up with our other events! You can find all of it on our website [utcg.club](https://utcg.club/).
